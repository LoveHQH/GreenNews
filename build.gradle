buildscript {
    apply from: './dependencies.gradle'

    ext.kotlin_version = '1.7.0'
    ext.compose_compiler_version = '1.2.0'
    ext.compose_version = '1.2.0-rc02'
    ext.compose_snapshot_version = ''
    ext.coroutines_version = '1.6.0'
    ext.accompanist_version = '0.24.12-rc'

    repositories {
        google()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$versions.hiltVersion"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$versions.spotlessGradle"
    }
}

plugins {
    id 'com.diffplug.spotless' version '6.7.0'
}

subprojects {
    repositories {
        google()
        mavenCentral()

        if (!compose_snapshot_version.isEmpty()) {
            maven { url "https://androidx.dev/snapshots/builds/" +
                    "${compose_snapshot_version}/artifacts/repository/" }
        }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint("0.45.2")
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }
}